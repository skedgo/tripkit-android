apply plugin: "com.android.library"
apply plugin: "com.github.dcendents.android-maven"
apply plugin: 'com.neenbedankt.android-apt'

group = "com.skedgo.sdk"
version = "0.0.1"

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.3"

  defaultConfig {
    minSdkVersion 10
    targetSdkVersion 22
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  lintOptions {
    abortOnError false
  }

  packagingOptions {
    // To avoid conflicts between Mockito and AssertJ.
    exclude 'LICENSE'
    exclude 'NOTICE'
    exclude 'asm-license.txt'

    // To avoid conflicts caused by Espresso's integration.
    exclude 'LICENSE.txt'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
}

task(generateStrings, type: org.gradle.api.tasks.JavaExec) {
  classpath(files('XLIFFtoAndroidConverter.jar'))
  main('com.skedgo.tripgo.tools.android.Main')
  args(['./src/main/res', '../translations', 'void',
        'en#es#de#fi#fr#nl#pt#it#zh-Hant#zh-Hans', 'iOS.xliff', 'android_localizable_strings.xliff'])
}

dependencies {
  compile fileTree(include: '*.jar', dir: 'libs')
  // To use Mockito in Android's instrumentation tests.
  androidTestCompile 'org.assertj:assertj-core:1.7.0'
  androidTestCompile 'org.mockito:mockito-core:1.10.5'
  androidTestCompile 'com.crittercism.dexmaker:dexmaker:1.4'
  androidTestCompile 'com.crittercism.dexmaker:dexmaker-mockito:1.4'
  androidTestCompile 'com.crittercism.dexmaker:dexmaker-dx:1.4'
  // To take advantage of AndroidJUnitRunner.
  androidTestCompile 'com.android.support.test:runner:0.4.1'
  androidTestCompile 'com.android.support.test:rules:0.4.1'
  testCompile 'junit:junit:4.12'
  testCompile 'org.assertj:assertj-core:1.7.0'
  testCompile 'org.mockito:mockito-core:1.10.19'
  testCompile 'org.robolectric:robolectric:3.1'
  compile 'com.android.support:support-annotations:23.3.0'
  compile 'com.google.android.gms:play-services-maps:8.4.0'
  // A folk of 'com.google.maps.android:android-maps-utils'
  // containing only bare essentials for TripGo.
  // Repo: https://github.com/skedgo/android-maps-utils
  compile('com.skedgo.android:maps-utils:0.0.1') {
    // We've already included 'play-services-maps'.
    // References:
    // * https://github.com/googlemaps/android-maps-utils/issues/138
    // * https://github.com/googlemaps/android-maps-utils/blob/master/library/build.gradle
    exclude(group: 'com.google.android.gms', module: 'play-services-maps')
  }

  compile 'net.danlew:android.joda:2.9.3.1'
  compile 'io.reactivex:rxjava:1.1.2'
  compile('io.reactivex:rxandroid:0.24.0') { exclude module: 'rxjava' }
  compile 'com.google.code.gson:gson:2.6.1'
  compile 'com.squareup.okhttp:okhttp:2.4.0'
  compile 'com.squareup.retrofit:retrofit:1.9.0'
  // To simplify implementation of Comparators and collection assertion.
  // This is a folk of 'org.apache.commons:commons-collections4'
  // containing only bare essentials for TripGo.
  // Source code is located at /libraries/commons-collections.
  compile 'com.skedgo.commons:commons-collections:0.0.1'
  apt "org.immutables:value:2.1.18"
  apt "org.immutables:gson:2.1.18"
  provided 'org.immutables:gson:2.1.18'
  provided 'org.immutables:value:2.1.18'
  provided 'org.immutables:builder:2.1.18'
}

/**
 * Run with 'gradlew clean test assemble uploadArchives'
 */
uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: "file://${TripKitMaven}")
    }
  }
}