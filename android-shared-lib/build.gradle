apply plugin: "com.android.library"
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// For https://github.com/dcendents/android-maven-gradle-plugin.
group = 'com.skedgo'
version = '1.3'

android {
  compileSdkVersion 24
  buildToolsVersion '25.0.2'

  defaultConfig {
    minSdkVersion 10
    targetSdkVersion 22
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  lintOptions {
    checkReleaseBuilds true
    abortOnError false
  }
}

task(generateAndroidStrings, type: org.gradle.api.tasks.JavaExec) {
  classpath(files('XLIFFtoAndroidConverter.jar'))
  main('com.skedgo.tripgo.tools.android.Main')
  args(['./src/main/res', '../../libraries/translations', 'void',
        // Format: dirSrc:dirDest#dirSrc:dirDest#...
        'en:en#es-ES:es#de-DE:de#fi-FI:fi#fr-FR:fr#nl-NL:nl#pt-PT:pt#it-IT:it#zh-CN:zh-Hans#zh-TW:zh-Hant#da-DK:da#ko-KR:ko',
        // Format: xliffSourceFile:fileElementType | xliffSourceFile (all file elements parsed)
        'android_localizable_strings.xliff'])
}

task(generateiOSStrings, type: org.gradle.api.tasks.JavaExec) {
  classpath(files('XLIFFtoAndroidConverter.jar'))
  main('com.skedgo.tripgo.tools.android.Main')
  args(['./src/main/res', '../../libraries/translations/Translations/4_FromCrowdin', 'void',
        // Format: dirSrc:dirDest#dirSrc:dirDest#...
        'en:en#es_ES:es#de:de#fi:fi#fr:fr#nl:nl#pt_PT:pt#it:it#zh_Hans:zh-Hans#zh_Hant:zh-Hant#da:da#ko:ko',
        // Format: xliffSourceFile:fileElementType | xliffSourceFile (all file elements parsed)
        'iOS.xliff'])
}

dependencies {
  androidTestCompile 'org.assertj:assertj-core:1.7.0'
  androidTestCompile 'com.android.support.test:runner:0.5'
  androidTestCompile 'com.android.support.test:rules:0.5'

  testCompile 'junit:junit:4.12'
  testCompile 'org.assertj:assertj-core:1.7.0'
  testCompile 'org.mockito:mockito-core:1.10.19'
  testCompile 'org.robolectric:robolectric:3.1.2'
  testCompile 'commons-io:commons-io:2.5'

  // See https://github.com/skedgo/android-maps-utils.
  compile 'com.github.skedgo:android-maps-utils:241fc89'
  compile 'com.android.support:support-annotations:24.2.1'
  compile 'com.google.android.gms:play-services-maps:9.6.1'
  compile 'net.danlew:android.joda:2.9.3.1'
  compile 'io.reactivex:rxjava:1.2.4'
  compile 'io.reactivex:rxandroid:1.2.1', { exclude module: 'rxjava' }
  compile 'com.google.code.gson:gson:2.6.1'
  compile 'com.github.skedgo:commons-collections:v1.0'

  // To generate immutable models.
  // See more at http://immutables.github.io/.
  annotationProcessor 'org.immutables:value:2.3.9'
  provided 'org.immutables:value:2.3.9:annotations'
  provided 'org.immutables:builder:2.3.9'
  provided 'org.immutables:gson:2.3.9:annotations'
}

install {
  repositories.mavenInstaller {
    pom.project {
      name 'TripKit Android'
      description 'Android Shared Lib'
      url 'https://github.com/skedgo/android-shared-lib.git'
      inceptionYear '2013'

      packaging 'aar'
      groupId project.group
      artifactId 'android-shared-lib'
      version project.version

      licenses {
        license {
          name 'The MIT License'
          url 'https://spdx.org/licenses/MIT#licenseText'
          distribution 'repo'
        }
      }
      developers {
        developer {
          name 'SkedGo'
        }
      }
    }
  }
}

bintray {
  user = BINTRAY_USER
  key = BINTRAY_KEY
  configurations = ['archives']
  pkg {
    repo = 'tripkit-android'
    name = 'android-shared-lib'
    userOrg = 'skedgo'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/skedgo/android-shared-lib.git'
    version {
      name = project.version
      desc = "Android Shared Lib $project.version"
      released = new Date()
      vcsTag = project.version
    }
  }
}

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: "file://${tripkitAndroidMaven}")
    }
  }
}
