apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'kotlin-parcelize'

android {

    namespace 'com.skedgo.sqlite'

    compileSdkVersion versions.compileSdkVersion
    buildToolsVersion "33.0.0"


    defaultConfig {
        minSdkVersion versions.proMinSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode 2
        versionName "2.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        staging {
            debuggable true
        }
    }

    publishing {
        singleVariant("release") {
            // if you don't want sources/javadoc, remove these lines
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation "com.android.support:support-annotations:28.0.0"
    implementation libs.rxjava2
}

group = tripkitGroup
version = tripkitVersion

publishing {
    repositories {
        maven {
            url = "file://${localMaven}"
            //url = publishMavenUrl
            credentials {
                username = publishMavenUsername
                password = publishMavenToken
            }
        }
    }

    publications {
        release(MavenPublication) {
            groupId = group
            version = version
            artifactId = 'sqliteutils'
            afterEvaluate {
                from components.release
            }
            pom {
                licenses {
                    license {
                        name = 'Commercial'
                    }
                }
            }
            pom.withXml {
                asNode().dependencies.'*'
                        .findAll() {
                            (it.scope.text() == 'runtime' || it.scope.text() == 'compile') &&
                                    project.configurations.implementation.allDependencies.find {
                                        dep -> dep.name == it.artifactId.text()
                                    }
                        }.each {
                    it.appendNode("type", "aar")
                }
            }
        }
    }
}

