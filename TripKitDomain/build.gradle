apply plugin: "kotlin"
apply plugin: "kotlin-kapt"
apply plugin: 'java'
apply plugin: 'maven-publish'

// To fix error of converting bytecode to dex.
targetCompatibility = '1.7'
sourceCompatibility = '1.7'

dependencies {
  testImplementation libs.junit
  testImplementation libs.assertjCore
  testImplementation libs.mockitoCore
  testImplementation libs.mockitoKotlin

  implementation libs.jodaTime
  implementation libs.rxjava
  implementation libs.dagger
  kapt libs.daggerCompiler

  implementation libs.kotlin
  kapt libs.value
  compileOnly libs.immutablesAndroidStub
  compileOnly libs.value
  implementation libs.koptional
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

group = tripkitGroup
version = tripkitVersion

publishing {
  repositories {
    maven { url = "file://${localMaven}" }
  }
  publications {
    mavenJava(MavenPublication) {
      groupId = group
      version = version
      from components.java
    }
  }
}