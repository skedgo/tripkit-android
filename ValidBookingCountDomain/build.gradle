apply plugin: "kotlin"
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'kotlin-kapt'

// To fix error of converting bytecode to dex.
targetCompatibility = '1.7'
sourceCompatibility = '1.7'

dependencies {
  testImplementation libs.junit
  testImplementation libs.assertjCore
  testImplementation libs.mockitoCore
  testImplementation libs.mockitoKotlin
  testImplementation project(":AccountDomain")

  implementation libs.kotlin
  implementation libs.rxjava
  implementation libs.dagger
  kapt libs.daggerCompiler

  // Use `compileOnly` to achieve similar effect of `provided` scope.
  // See more https://blog.gradle.org/introducing-compile-only-dependencies.
  compileOnly project(":AccountDomain")
}


task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

group = tripkitGroup
version = tripkitVersion

publishing {
  repositories {
    maven { url = "file://${localMaven}" }
  }
  publications {
    mavenAar(MavenPublication) {
      groupId = group
      version = version
      from components.java
    }
  }
}