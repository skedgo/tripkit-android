apply plugin: "com.android.library"
// To publish via JitPack.
// See https://jitpack.io/docs/ANDROID/.
apply plugin: "com.github.dcendents.android-maven"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"

// For https://github.com/dcendents/android-maven-gradle-plugin.
group = "com.skedgo.tripkit"
version = "1.2"

android {
  compileSdkVersion versions.compileSdkVersion
  buildToolsVersion versions.buildToolsVersion

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 27
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  packagingOptions {
    // To avoid conflicts with ASL.
    // We don't utilize ServiceLoader, so this is unneeded.
    exclude 'META-INF/services/com.google.gson.TypeAdapterFactory'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }

  lintOptions {
    checkReleaseBuilds true
    abortOnError false
  }
}

// Generation relative to tripgo! Add translations repo to tripkit?
task(generateTripkitStrings, type: org.gradle.api.tasks.JavaExec) {
  classpath(files('IOStoAndroidStringsConverter.jar'))
  main('com.skedgo.tripgo.tools.android.Main')
  args(['./src/main/res', '../../libraries/translations', 'void',
        'en#es#de#fi#pt#zh-Hant#zh-Hans', 'Tripkit.strings'])
}

dependencies {
  testImplementation libs.junit
  testImplementation libs.assertjCore
  testImplementation libs.robolectric
  testImplementation libs.mockitoCore
  testImplementation libs.mockitoKotlin, { exclude group: 'org.jetbrains.kotlin' }
  testImplementation libs.kluent
  testImplementation 'com.github.thuytrinh:MockWebServerRule:v1.0'
  testImplementation libs.kluent

  androidTestImplementation "com.android.support.test:runner:0.5"
  androidTestImplementation "com.android.support.test:rules:0.5"
  androidTestImplementation libs.assertjCore

  implementation libs.kotlin
  implementation libs.jodaTimeAndroid
  implementation 'com.github.skedgo:commons-collections:v1.0'

  implementation 'com.github.skedgo:sqlite-utils:v1.0'
  implementation libs.okHttp
  implementation libs.retrofit
  implementation libs.retrofitAdapterRxJava
  implementation libs.retrofitConverterGson
  implementation libs.playServicesMaps
  implementation libs.rxjava
  implementation libs.rxAndroid
  implementation libs.androidMapsUtils
  implementation 'com.firebase:firebase-jobdispatcher:0.8.5'

  implementation libs.dagger
  kapt libs.daggerCompiler
  kapt libs.value
  compileOnly libs.valueAnnotations
  compileOnly libs.builderAnnotations
  implementation libs.immutablesGson
  api project(':CommonCoreLegacy')
  api project(':TripKitDomain')
  api project(':TripKitDomainLegacy')
  api project(':TripKitData')
}



uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: "file://${localMaven}")
    }
  }
}
