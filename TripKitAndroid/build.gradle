apply plugin: "com.android.library"
// To publish via JitPack.
// See https://jitpack.io/docs/ANDROID/.
//apply plugin: "com.github.dcendents.android-maven"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"

android {
  compileSdkVersion versions.compileSdkVersion

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion versions.targetSdkVersion
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  packagingOptions {
    // To avoid conflicts with ASL.
    // We don't utilize ServiceLoader, so this is unneeded.
    exclude 'META-INF/services/com.google.gson.TypeAdapterFactory'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }

  lintOptions {
    checkReleaseBuilds true
    abortOnError false
  }
}

// Generation relative to tripgo! Add translations repo to tripkit?
task(generateTripkitStrings, type: org.gradle.api.tasks.JavaExec) {
  classpath(files('IOStoAndroidStringsConverter.jar'))
  main('com.skedgo.tripgo.tools.android.Main')
  args(['./src/main/res', '../../libraries/translations', 'void',
        'en#es#de#fi#pt#zh-Hant#zh-Hans', 'Tripkit.strings'])
}

dependencies {
  testImplementation libs.junit
  testImplementation libs.assertjCore
  testImplementation libs.robolectric
  testImplementation libs.mockitoCore
  testImplementation libs.mockitoKotlin, { exclude group: 'org.jetbrains.kotlin' }
  testImplementation libs.kluent
  testImplementation 'com.github.thuytrinh:MockWebServerRule:v1.0'
  testImplementation libs.kluent

  androidTestImplementation "androidx.test:runner:1.1.0"
  androidTestImplementation "androidx.test:rules:1.1.0"
  androidTestImplementation libs.assertjCore

  implementation libs.kotlin
  implementation libs.jodaTimeAndroid
  implementation 'com.github.skedgo:commons-collections:v1.0'

  implementation 'com.github.skedgo:sqlite-utils:v1.0'
  implementation libs.okHttp
  implementation libs.retrofit
  implementation libs.retrofitAdapterRxJava
  implementation libs.retrofitConverterGson

  implementation libs.rxjava
  implementation libs.rxAndroid
  implementation 'com.firebase:firebase-jobdispatcher:0.8.5'

  implementation libs.dagger
  kapt libs.daggerCompiler
  kapt libs.value
  compileOnly libs.valueAnnotations
  compileOnly libs.builderAnnotations
  implementation libs.immutablesGson
  api project(':CommonCoreLegacy')
  api project(':TripKitDomain')
  api project(':TripKitDomainLegacy')
  api project(':TripKitData')
}

// build a jar with source files
task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

task javadoc(type: Javadoc) {
  failOnError  false
  source = android.sourceSets.main.java.sourceFiles
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
  classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

group = tripkitGroup
version = tripkitVersion

//uploadArchives {
//  repositories {
//    mavenDeployer {
//      repository(url: "file://${localMaven}")
//      pom.groupId = tripkitGroup
//      pom.version = tripkitVersion
//    }
//  }
//}