apply plugin: "com.android.library"
// To publish via JitPack.
// See https://jitpack.io/docs/ANDROID/.
apply plugin: "com.github.dcendents.android-maven"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"

// For https://github.com/dcendents/android-maven-gradle-plugin.
group = "com.skedgo.tripkit"

android {
  compileSdkVersion libVersions.compileSdkVersion
  buildToolsVersion libVersions.buildToolsVersion

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 25
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  packagingOptions {
    // To avoid conflicts with ASL.
    // We don't utilize ServiceLoader, so this is unneeded.
    exclude 'META-INF/services/com.google.gson.TypeAdapterFactory'
  }

  lintOptions {
    checkReleaseBuilds true
    abortOnError false
  }
}

// Generation relative to tripgo! Add translations repo to tripkit?
task(generateTripkitStrings, type: org.gradle.api.tasks.JavaExec) {
  classpath(files('IOStoAndroidStringsConverter.jar'))
  main('com.skedgo.tripgo.tools.android.Main')
  args(['./src/main/res', '../../libraries/translations', 'void',
        'en#es#de#fi#pt#zh-Hant#zh-Hans', 'Tripkit.strings'])
}

dependencies {
  testCompile libs.junit
  testCompile libs.assertjCore
  testCompile libs.robolectric
  testCompile libs.mockitoCore
  testCompile libs.mockitoKotlin, { exclude group: 'org.jetbrains.kotlin' }
  testCompile libs.kluent
  testCompile 'com.github.thuytrinh:MockWebServerRule:v1.0'
  testCompile libs.kluent

  androidTestCompile "com.android.support.test:runner:0.5"
  androidTestCompile "com.android.support.test:rules:0.5"
  androidTestCompile libs.assertjCore

  compile libs.kotlin
  compile libs.playServicesGcm

  compile 'com.github.skedgo:sqlite-utils:v1.0'
  compile libs.okHttp
  compile libs.retrofitConverterGson
  compile libs.retrofitAdapterRxJava
  compile libs.retrofit

  compile libs.dagger
  kapt libs.daggerCompiler
  kapt libs.value
  provided libs.valueAnnotations
  provided libs.builderAnnotations
  provided libs.gsonAnnotations
  compile project(':CommonCoreLegacy')
  compile project(':TripKitDomain')
  compile project(':TripKitData')
}
