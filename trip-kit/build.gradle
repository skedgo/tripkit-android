apply plugin: "com.android.library"
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'kotlin-android'

// For https://github.com/dcendents/android-maven-gradle-plugin.
group = 'com.skedgo'
version = '1.1'

android {
  compileSdkVersion 25
  buildToolsVersion '25.0.2'

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 25
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  packagingOptions {
    // To avoid conflicts with ASL.
    // We don't utilize ServiceLoader, so this is unneeded.
    exclude 'META-INF/services/com.google.gson.TypeAdapterFactory'
  }

  lintOptions {
    checkReleaseBuilds true
    abortOnError false
  }
}

// Generation relative to tripgo! Add translations repo to tripkit?
task(generateTripkitStrings, type: org.gradle.api.tasks.JavaExec) {
  classpath(files('IOStoAndroidStringsConverter.jar'))
  main('com.skedgo.tripgo.tools.android.Main')
  args(['./src/main/res', '../../libraries/translations', 'void',
        'en#es#de#fi#pt#zh-Hant#zh-Hans', 'Tripkit.strings'])
}

dependencies {
  testCompile 'junit:junit:4.12'
  testCompile 'org.assertj:assertj-core:1.7.0'
  testCompile 'org.mockito:mockito-core:1.10.19'
  testCompile 'com.github.thuytrinh:MockWebServerRule:v1.0'
  testCompile 'org.robolectric:robolectric:3.1.2'

  androidTestCompile "com.android.support.test:runner:0.5"
  androidTestCompile "com.android.support.test:rules:0.5"
  androidTestCompile "org.assertj:assertj-core:1.7.0"

  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
  compile "com.google.android.gms:play-services-gcm:10.0.1"

  compile 'com.github.skedgo:sqlite-utils:v1.0'
  compile "com.squareup.okhttp3:okhttp:$okHttpVersion"
  compile "com.squareup.retrofit2:converter-gson:2.0.2"
  compile "com.squareup.retrofit2:adapter-rxjava:2.0.2"
  compile "com.squareup.retrofit2:retrofit:2.0.2"

  compile 'com.google.dagger:dagger:2.7'
  kapt 'com.google.dagger:dagger-compiler:2.7'

  // To generate immutable models.
  // See more at http://immutables.github.io/.
  kapt "org.immutables:value:$immutablesVersion"
  provided "org.immutables:value:$immutablesVersion:annotations"
  provided "org.immutables:builder:$immutablesVersion"
  provided "org.immutables:gson:$immutablesVersion:annotations"

  compile project(':android-shared-lib')
  compile project(':ConfigurationDomain')
  compile project(':ConfigurationData')
}

install {
  repositories.mavenInstaller {
    pom.project {
      name 'TripKit Android'
      description 'TripKit Android'
      url 'https://github.com/skedgo/tripkit-android.git'
      inceptionYear '2013'

      packaging 'aar'
      groupId project.group
      artifactId 'trip-kit'
      version project.version

      licenses {
        license {
          name 'The MIT License'
          url 'https://spdx.org/licenses/MIT#licenseText'
          distribution 'repo'
        }
      }
      developers {
        developer {
          name 'SkedGo'
        }
      }
    }
  }
}

bintray {
  user = BINTRAY_USER
  key = BINTRAY_KEY
  configurations = ['archives']
  pkg {
    repo = 'tripkit-android'
    name = 'tripkit'
    userOrg = 'skedgo'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/skedgo/tripkit-android.git'
    version {
      name = project.version
      desc = "TripKit Android $project.version"
      released = new Date()
      vcsTag = project.version
    }
  }
}

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: "file://${tripkitAndroidMaven}")
    }
  }
}
