buildscript {
  apply from: "dependencies.gradle"
  ext.kotlinVersion = libVersions.kotlinVersion
  ext.androidPluginVersion = libVersions.androidPluginVersion
  ext.okHttpVersion = libVersions.okHttpVersion

  repositories {
    jcenter()
    maven {
      url 'https://maven.google.com'
    }
  }

  dependencies {
    // See changelog: http://developer.android.com/tools/revisions/gradle-plugin.html.
    classpath "com.android.tools.build:gradle:$androidPluginVersion"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

    // To measure execution time of a method.
    classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'

    // To deploy AAR files to local maven.
    classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
  }
}

ext {
  tripKitAndroidMaven = "${rootDir}/maven/"
}

allprojects {
  apply from: "${rootDir}/dependencies.gradle"

  // A workaround for https://code.google.com/p/android/issues/detail?id=72430
  // and http://g.co/androidstudio/app-test-app-conflict.
  configurations.all {
    resolutionStrategy {
      force libs.rxjava
      force libs.okHttp
      force libs.mockWebServer
      force libs.retrofit
      force libs.retrofitAdapterRxJava
      force libs.retrofitConverterGson
    }
  }

  repositories {
    jcenter()
    maven { url "https://jitpack.io" }
    maven {
      url "http://skedgo.bintray.com/tripkit-android"
      credentials {
        username BINTRAY_USER
        password BINTRAY_KEY
      }
    }
    maven { url 'https://maven.google.com' }
  }
}
